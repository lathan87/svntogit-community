# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-gdspy
pkgver=1.6.7
pkgrel=1
pkgdesc="Python module for creating GDSII stream files, usually CAD layouts"
url="https://github.com/heitzmann/gdspy"
license=('custom:BSL')
arch=('x86_64')
depends=('python-numpy')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/heitzmann/gdspy/archive/v$pkgver.tar.gz")
sha512sums=('829c12a59227df41c8a6add5b86920df8d8fb9b15c6ac50ad15dc73d4554f2ce01e0ebe212ff8709977b7dfb2a1b82e3097af0a8e15b527f514c7e4b34141504')

build() {
  cd gdspy-$pkgver
  python setup.py build
}

check() {
  cd gdspy-$pkgver
  python setup.py pytest
}

package() {
  cd gdspy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
